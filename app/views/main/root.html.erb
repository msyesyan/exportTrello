<div id="lists">
</div>
<div id="output"></div>

<script type="text/javascript" src="https://api.trello.com/1/client.js?key=9115435cc98b67a65c3a6dcff606cb09"></script>

<script type="text/coffeescript">
  settings =
    applicationName: "ExportTrelloToWeekPapers"
    key: "9115435cc98b67a65c3a6dcff606cb09"
    boardId: "KDFzTG2T"
    i18n:
      hh: "健康到家"
      zbh: "闸北医疗"
      ngb: "智慧闵行"
      cam: "智能摄像头"
      zgav: "张江视听"
      hxl: "沪杏图书馆"

  boardId = settings.boardId

  initList = ->
    $("#list").empty()
    Trello.boards.get boardId, { lists: "open" }, (board) ->
      $("<h2/>").html("Lists of " + board.name).appendTo('#lists')

      for list in board.lists
        $("<div/>",
          class: "list"
          "data-id": list.id
          text: list.name
        ).appendTo "#lists"

      $(".list").click ->
        $("#output").html("loading....")
        $(this).addClass("active").siblings().removeClass("active")
        Trello.lists.get($(this).data("id"), cards: "open", (list) ->
          projects = {}

          for card in list.cards
            projectKeyTitle = card.name.match(/\{(.*)}/)[1]
            projects[projectKeyTitle] = []  unless projects[projectKeyTitle]
            projects[projectKeyTitle].push $.trim(card.name.replace(/\{.*\}|\(.*\)|\[.*\]/g, ""))

          $("#output").empty()
          for project of projects
            $("<h2 />",
              text: settings.i18n[project] or project
            ).appendTo "#output"

            for cardContent in projects[project]
              content = if cardContent.match(/解决|问题|修复|无效|错误/g) then "[解决] " + cardContent else "[完成] " + cardContent
              $("<div/>",
                text: content
              ).appendTo "#output"
        )
    , (error) ->
      alert error

  Trello.authorize
    interactive: false,
    success: initList,
    error: ->
      Trello.authorize
        type: "popup",
        name: settings.applicationName
        success: initList
</script>
</html>